description "Jwala is a web application that provides management for a group of Tomcat servers. Jwala is capable of creating and persisting definitions of Group instances, and exposes a RESTful interface to do so. The definition of a Group includes Web Apps, JVMs, Web Servers, and Resources."
ext {
    versions = [
            junit                 : "4.11",
            pmd                   : "5.1.1",
            spring                : "4.1.8.RELEASE",
            spring_security       : "4.1.1.RELEASE",
            jackson               : "1.4.2",
            jpa                   : "2.4.0",
            h2                    : "1.3.176",
            cxf                   : "3.0.0-milestone1",
            javax_ws_rs_api       : "2.0",
            mockito               : "1.9.5",
            log4j                 : '1.2.17',
            slf4j                 : '1.7.5',
            slf4j_log4j12         : '1.7.5',
            javax_servlet_api     : '3.0.1',
            csa                   : "4.0.0700.00",
            commons_fileupload    : '1.3.1',
            jodaTime              : "2.2",
            commons_lang          : "3.3.2",
            commons_logging       : "1.1.1",
            httpcomponents_core   : "4.3.3",
            httpcomponents_client : "4.3.3",
            tomcat                : '7.0.55',
            ant                   : '1.8.2',
            groovy                : '2.4.3',
            jacoco                : '0.7.7.201606060606',
            devbundle             : '1.1-SNAPSHOT',
            objenesis             : '1.0',
            jgroups               : '3.6.6.Final',
            commons_io            : '2.4',
            jkeylockmanager       : '1.2.0',
            jackson               : '1.9.13',
            babelcli              : '6.6.4',
            babeltransform        : '6.5.0'
    ]
}
repositories {
    mavenCentral()
}

task(clean) << {
    delete "build/jacoco"
}

subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "pmd"
    apply plugin: "eclipse"

    sourceCompatibility = 1.7
    group = "com.cerner.jwala"
    version = project.properties['buildVersion']

    plugins.withType(MavenPublishPlugin).whenPluginAdded {

        publishing {
            repositories {
                maven {
                    url project.properties['publishRepoUrl']
                    credentials {
                        username project.properties['publishRepoUser']
                        password project.properties['publishRepoPassword']
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    if (project.name.equals('jwala-webapp')) {
                        from components.web
                    }else if (project.name.equals('jwala-tomcat')) {
                        artifact zipJwalaTomcat
                    }else {
                        from components.java
                    }
                    if (project.name.equals('jwala-persistence')) {
                        artifact("drop.sql") {
                            classifier "drop"
                            extension "sql"
                        }
                        artifact("create.sql") {
                            classifier "create"
                            extension "sql"
                        }
                    }
                }
            }
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: "$versions.slf4j"
        compile group: 'org.apache.commons', name: 'commons-lang3', version: "$versions.commons_lang"

        testCompile "junit:junit:4.11"
        testCompile group: 'org.mockito', name: 'mockito-core', version: "$versions.mockito"

        pmd group: "net.sourceforge.pmd", name: "pmd", version: "$versions.pmd"

    }

    jar {
        manifest {
            attributes(
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.version,
                    "Specification-Number": (null == System.getProperty("gitCommitId") ? "" : "." + System.getProperty("gitCommitId")),
                    "Build-Number": (null == System.getProperty("buildNumber") ? "" : "." + System.getProperty("buildNumber")),
                    "Built-By": System.getProperty("user.name"),
                    "Built-JDK": System.getProperty("java.version"),
                    "Built-Host": InetAddress.getLocalHost(),
                    "Build-Time-ISO-8601": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            )
        }
    }

    pmdMain {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    pmdTest {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    jacoco {
        toolVersion = "$project.versions.jacoco"
    }

    jacocoTestReport {
        reports {
            xml.enabled = false
            csv.enabled = false
            html.enabled = true
            html.destination "${buildDir}/jacocoHtml"
        }
    }

    test {
        systemProperties['run.test.types'] = 'integration'
        systemProperties['spring.profiles.active'] = 'integration'
    }

    task mergeTestCodeCoverageResults(type: JacocoMerge) {
        description = 'Merge test code coverage results from all java projects'
        destinationFile = file("${project.rootProject.buildDir.path}/jacoco/accum.exec")
        def testFile = file("${project.buildDir.path}/jacoco/test.exec")
        onlyIf {
            testFile.exists()
        }
        executionData {
            if (destinationFile.exists()) {
                return files(destinationFile, testFile);
            }
            else return files(testFile);
        }
    }

    test.finalizedBy mergeTestCodeCoverageResults
}

task(createRelease) << {
    def gradleProperties = file("gradle.properties")
    def lines = gradleProperties.readLines()
    def line = lines.first()
    def versions = line.split("=")
    def version = versions.last().replace("-SNAPSHOT", "")
    lines[0] = "buildVersion=" + version
    gradleProperties.withWriter {
        lines.eachWithIndex { l, i ->
            it.write(l + "\n")
        }
    }
}

task(createSnapshot) << {
    def gradleProperties = file("gradle.properties")
    def lines = gradleProperties.readLines()
    def line = lines.first()
    def versions = line.split("=")
    def version = versions.last().replace("-SNAPSHOT", "")
    def numbers = version.split("\\.")
    def newNumber = numbers.last().toInteger() + 1
    numbers[numbers.size() - 1] = newNumber.toString()
    version = new String()
    numbers.eachWithIndex { num, i ->
        if (i < numbers.size() - 1) {
            version = version + num + "."
        } else {
            version = version + num
        }
    }
    lines[0] = "buildVersion=" + version + "-SNAPSHOT"
    gradleProperties.withWriter {
        lines.eachWithIndex { l, i ->
            it.write(l + "\n")
        }
    }
}

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'de.undercouch:gradle-download-task:3.1.1'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.1"
    }
}




buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
    }
}

apply plugin: 'com.moowork.node'

/*** Sonar Runner: start ***/
apply plugin: "sonar-runner"

// Project wide SonarRunner properties definition
sonarRunner {
    sonarProperties {
        property 'sonar.host.url',  project.properties['sonarHostUrl']
        property 'sonar.jdbc.url', project.properties['sonarJDBCUrl']
        property 'sonar.jdbc.driverClassName', 'com.mysql.jdbc.Driver'
        property 'sonar.jdbc.username', 'sonar'
        property 'sonar.jdbc.password', 'sonar'
        property 'sonar.dynamicAnalysis', 'reuseReports'
        properties["sonar.projectKey"] = 'cto.tco.toc'
        properties["sonar.projectName"] = 'tomcat operations center'
        properties["sonar.projectVersion"] =  '1.0.0'
    }
}

// Sonar Runner requires sourceCompatibility 1.7 which is the reason why it's in its own subproject
subprojects {
    sourceCompatibility = 1.7

    version = project.properties['buildVersion']

    sonarRunner {
        sonarProperties {
            property "sonar.junit.reportsPath", test.reports.junitXml.destination   // will be fixed in Gradle 1.12
            property "sonar.jacoco.reportPath", project.rootProject.buildDir.path + "/jacoco/" + "accum.exec"
            property "sonar.jacoco.itReportPath", project.rootProject.buildDir.path + "/jacoco/" + "integration_test_jwala.exec"

        }
    }
}
/*** Sonar Runner: end ***/

apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property 'sonar.jacoco.reportPath', project.rootProject.buildDir.path + "/jacoco/" + "accum.exec"
        property 'sonar.jacoco.itReportPath', project.rootProject.buildDir.path + "/jacoco/" + "integration_test_jwala.exec"
        property 'sonar.host.url',  project.properties['sonarHostUrl']
        property 'sonar.jdbc.driverClassName', 'com.mysql.jdbc.Driver'
        property 'sonar.projectKey', 'cto.tco.toc'
        property 'sonar.projectName', 'tomcat operations center'
        property 'sonar.projectVersion', '1.0.0'
    }

}